PREFIX spkl: <http://speckle.systems/>
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX s223: <http://data.ashrae.org/standard223#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX meta: <http://meta>


# see query in  geometry.py
construct {
  ?this a s223:Luminaire.
  ?this rdfs:label ?number .
  ?this s223:hasConnectionPoint ?cpi.
  ?cpi  a s223:InletConnectionPoint.
  ?cpi s223:hasMedium s223:Medium-Electricity. #later we can add the value of voltage  
  ?this s223:hasConnectionPoint ?cpo.
  ?cpo  a s223:OutletConnectionPoint.  
  ?cpo s223:hasMedium s223:EM-Light.
}
where {
<<?this spkl:category  "Lighting Fixtures">> meta: <<?mi spkl:name "architecture/rooms and lighting fixtures">>. # TODO branchName could be used here
    ?this spkl:phaseCreated ?_.  # to say it's being 'used' in the plan and not part of the 'types'
    ?this spkl:elementId ?number.
    bind(uri(concat(str(?this),("/iconnection") )) as ?cpi).
    bind(uri(concat(str(?this),("/oconnection") )) as ?cpo).
}
