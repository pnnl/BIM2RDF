PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX s223: <http://data.ashrae.org/standard223#>
PREFIX spkl: <http://speckle.systems/> 


CONSTRUCT { 
?p s223:encloses ?d. 
} 
WHERE { 
?p a s223:PhysicalSpace.
	### relationship
    ?p rdfs:label ?rl.
    ?d spkl:parameters/spkl:ROOM_NAME/spkl:value ?_rn.
    # revit creates a new name if a room is divided by spaces
    # in this case, revit appends a dash, -, followed by a number
    # so the logic below takes the name before the dash if it exists
    # otherwise (no dash) it takes the name as is.
    # ISSUE: what if room name (actually) contains a dash?
    # TODO: geometry is the easiet solution instead of name mangling
    bind(if( STRBEFORE(?_rn, "-" ) , STRBEFORE(?_rn, "-" ) , ?_rn  ) as ?rn).
    filter(?rl = ?rn)
    ?d spkl:category  "Spaces".
    ###
    ?d a s223:DomainSpace. 
}